0 DIM FILE$(10),FILECOOR$(15)
1 FILE$="H6:F00.BIN":FILECOOR$="H6:COORDS.TXT"
2 OPEN #4,8,%0,FILECOOR$
10 DEG 
20 WIDTH=160:HEIGHT=192:SIDESIZE=WIDTH/2:OFFSET=5
30 LCX=SIDESIZE/2-OFFSET:RCX=SIDESIZE+(SIDESIZE/2)+OFFSET
40 LCY=WIDTH/3:RCY=LCY
50 ARMLEN=(RCX-LCX)/2
60 FRAME=0
110 FOR ANGLE=360 TO 1 STEP -(360/52)
111   GRAPHICS 15+16
115   CX=LCX:CY=LCY
120   DRAWCIRCLE=1
130   S=SIN(ANGLE+180):C=COS(ANGLE+180)
140   PX=LCX:PY=LCY-ARMLEN
150   PX=PX-CX:PY=PY-CY
160   NX=PX*C-PY*S:NY=PX*S+PY*C
170   NY=NY*2:REM RESPECT PIXEL ASPECT RATIO
180   PX=NX+CX:PY=NY+CY
185   NN = PX: GOSUB 2000
186   PUT #4, 44
187   NN = PY: GOSUB 2000
188   PUT #4, 10: PUT #4, 13
190   IF PX<0 THEN PX=0:DRAWCIRCLE=0
200   IF PY<0 THEN PY=0:DRAWCIRCLE=0
210   REM ? INT(CX);",";INT(CY);" --> ";INT(PX);",";INT(PY)
220   COLOR 2
230   PLOT CX,CY:DRAWTO PX,PY
240   COLOR 1
250   PLOT LCX,LCY:PLOT RCX,RCY
260   IF DRAWCIRCLE=1 THEN COLOR 3:CIRCLE PX,PY,2,4
315   CX=RCX:CY=RCY
320   DRAWCIRCLE=1
330   S=SIN(360-ANGLE+180):C=COS(360-ANGLE+180)
340   PX=CX:PY=CY-ARMLEN
350   PX=PX-CX:PY=PY-CY
360   NX=PX*C-PY*S:NY=PX*S+PY*C
370   NY=NY*2:REM RESPECT PIXEL ASPECT RATIO
380   PX=NX+CX:PY=NY+CY
390   IF PX>=WIDTH THEN PX=WIDTH-1:DRAWCIRCLE=0
400   IF PY<0 THEN PY=0:DRAWCIRCLE=0
410   REM ? INT(CX);",";INT(CY);" --> ";INT(PX);",";INT(PY)
420   COLOR 2
430   PLOT CX,CY:DRAWTO PX,PY
440   COLOR 1
450   PLOT LCX,LCY:PLOT RCX,RCY
460   IF DRAWCIRCLE=1 THEN COLOR 3:CIRCLE PX,PY,2,4
498   FILE$(5,5)=CHR$(48+TRUNC(FRAME/10))
499   FILE$(6,6)=CHR$(48+TRUNC(FRAME MOD 10))
500   OPEN #5,8,%0,FILE$
510   BPUT #5,$A150,7680
520   CLOSE #5
530   FRAME=FRAME+1
999 NEXT ANGLE
1000 CLOSE #4
1010 GOTO 1010
2000 IF NN < 0 THEN PUT #4, 45: NN = -NN
2010 HUN = INT(NN / 100)
2020 PUT #4, HUN+48
2030 NN = NN - HUN * 100
2040 DZIES = INT(NN / 10)
2050 PUT #4, DZIES+48
2060 NN = NN - DZIES * 10
2070 PUT #4, NN+48
2080 RETURN
